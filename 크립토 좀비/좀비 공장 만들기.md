## 컨트랙트

컨트랙트란 이더리운 애플리케이션의 기본적인 구성 요소로, 모든 변수와 함수는 어느 한 컨트랙트에 속한다.

## **Version Pragma**

모든 솔리디티 소스 코드는 "version pragma"로 시작해야 하며 해당 코드가 이용해야 하는 솔리디티 버전을 선언하는 것이다.

이를 통해 이후에 새로운 컴파일러 버전이 나와도 기존 코드가 깨지지 않도록 예방한다.

<aside>
⭐ 실습

우리의 좀비 군대 생성을 시작하기 위해 `ZombieFactory`라는 기본 컨트랙트를 생성해 보세!

1. 우측 박스에 우리 컨트랙트가 솔리디티 버전 `0.4.19`를 쓸 수 있도록 설정한다.
2. `ZombieFactory`라는 빈 컨트랙트를 생성한다.

```solidity
pragma solidity ^0.4.19; //1. 여기에 솔리디티 버전 적기

//2. 여기에 컨트랙트 생성
contract ZombieFactory {
}
```

</aside>

## 상태 변수 & 정수 자료형

### 상태 변수

상태 변수는 컨트랙트 저장소에 영구적으로 저장된다.(이더리움 블록체인에 기록)

```solidity
contract Example {
  // 이 변수는 블록체인에 영구적으로 저장된다
  uint myUnsignedInteger = 100;
}
```

### uint

부호 없는 정수 자료형으로, **값이 음수가 아니어야 한다.** 

- 솔리디티에서 `uint`는 실제로 `uint256` 이다.
- `uint8`, `uint16`, `uint32` 등과 같이 uint를 더 적은 비트로 선언할 수도 있지만 특수한 경우가 아니라면 일반적으로 단순히 `uint`를 사용한다.

### int

부호 있는 정수를 위한 자료형

<aside>
⭐ 실습

우리의 좀비 DNA는 16자리 숫자로 결정될 걸세.

`dnaDigits`라는 `uint`를 선언하고 `16`이라는 값을 배정해 보게.

```solidity
pragma solidity ^0.4.19;

contract ZombieFactory {
    uint dnaDigits = 16;
}

```

</aside>

## 수학 연산

- 덧셈 : `x + y`
- 뺄셈 : `x - y`
- 곱셈 : `x * y`
- 나눗셈 : `x / y`
- 나머지 : `x % y`
- 지수 : `x ** y`

```solidity
uint x = 5 ** 2; // 즉, 5^2 = 25
```

<aside>
⭐ 실습

우리의 좀비 DNA가 16자리 숫자가 되도록 하기 위해 또다른 `unit`형 변수를 생성하고 10^16 값을 배정하세. 이로써 이 값을 이후 모듈로 연산자 `%`와 함께 이용하여 16자리보다 큰 수를 16자리 숫자로 줄일 수 있네.

1. `dnaModulus`라는 `uint`형 변수를 생성하고 **10의 `dnaDigits`승**을 배정한다.

```solidity
pragma solidity ^0.4.19;

contract ZombieFactory {
    uint dnaDigits = 16;
		uint dnaModulus = 10 ** dnaDigits;
}
```

</aside>

## 구조체

```solidity
struct Person {
  uint age;
  string name;
}
```

## string 자료형

임의의 길이를 가진 UTF-8 데이터를 위해 활용

`string greeting = "Hello world!"` 

<aside>
⭐ 실습

우리 앱에서 좀비 몇 마리를 생성하기를 원할 것이네! 좀비들이 다양한 특성을 가질 것이니 구조체를 활용하기에 안성맞춤이군.

1. `Zombie`라는 `struct`를 생성한다.
2. 우리의 `Zombie` 구조체는 `name` (`string`형)과 `dna` (`uint`형)이라는 2가지 특성을 가진다.

```solidity
pragma solidity ^0.4.19;

contract ZombieFactory {

    uint dnaDigits = 16;
    uint dnaModulus = 10 ** dnaDigits;

    struct Zombie {
        string name;
        uint dna;
    }

}
```

</aside>

## 배열

### 정적 배열 & 동적 배열

```solidity
// 2개의 원소를 담을 수 있는 고정 길이의 배열
uint[2] fixedArray;
// 5개의 스트링을 담을 수 있는 고정 길이의 배열
string[5] stringArray;
// 동적 배열은 고정된 크기가 없으며 계속 크기가 커질 수 있음
uint[] dynamicArray;
// 구조체로 생성한 동적 배열로, 원소를 계속 추가할 수 있다.
Person[] people;
```

### Public 배열

`public`으로 배열을 선언하면 ***getter*** 메소드가 자동적으로 생성된다. 
이를 통해 다른 컨트랙트들이 해당 배열을 읽을 수 있게 된다. (쓸 수는 없음)
컨트랙트에 공개 데이터를 저장할 때 유용한 패턴이다.

```solidity
Person[] public people;
```

<aside>
⭐ 실습

우리 앱에 좀비 군대를 저장하고 싶네. 그리고 우리 좀비들을 다른 앱에 자랑하고 싶네. 그러니 좀비 군대 저장소를 public으로 해야 하네.

1. `Zombie` ***구조체***의 public 배열을 생성하고 이름을 `zombies`로 한다.

```solidity
pragma solidity ^0.4.19;

contract ZombieFactory {

    uint dnaDigits = 16;
    uint dnaModulus = 10 ** dnaDigits;

    struct Zombie {
        string name;
        uint dna;
    }

    Zombie[] public zombies;

}
```

</aside>

## 함수 선언

함수 인자명을 언더스코어(`_`)로 시작해서 전역 변수와 구별하는 것이 관례

```solidity
// 함수 선언(eatHamburgers라는 함수로, string과 uint 2개의 인자를 전달)
function eatHamburgers(string _name, uint _amount) {

}

// 함수 호출
eatHamburgers("vitalik", 100);
```

<aside>
⭐ 실습

우리 앱에서 좀비들을 생성할 수 있을 필요가 있을 거네. 이를 위한 함수를 생성해 보세.

1. `createZombie`라는 함수를 생성한다. 이 함수는 다음 2개의 인자를 전달받아야 한다: **_name** (`string`형)과 **_dna** (`uint`형).

함수의 내용은 지금으로선 비어 두면 되네. 나중에 채울 것이니까.

```solidity
pragma solidity ^0.4.19;

contract ZombieFactory {

    uint dnaDigits = 16;
    uint dnaModulus = 10 ** dnaDigits;

    struct Zombie {
        string name;
        uint dna;
    }

    Zombie[] public zombies;

    function createZombie(string _name, uint _dna){
        
    }

}
```

</aside>

## 구조체와 배열 활용하기

### 구조체 생성해서 배열에 추가하기

```solidity
// 새로운 사람을 생성
Person satoshi = Person(172, "Satoshi");

// 이 사람을 배열에 추가
people.push(satoshi);

// 한 줄로 표현하기
people.push(Person(16, "Vitalik"));
```

### `array.push()`

무언가를 배열의 **끝**에 추가해서 모든 원소가 순서를 유지한다.

```solidity
uint[] numbers;
numbers.push(5);
numbers.push(10);
numbers.push(15);
// numbers 배열은 [5, 10, 15]과 같다.
```

<aside>
⭐ 실습

createZombie 함수가 무언가 할 수 있도록 만들어 보세!

1. 함수에 코드를 넣어 새로운 `Zombie`를 생성하여 `zombies` 배열에 추가하도록 한다. 새로운 좀비를 위한 `name`과 `dna`는 `createZombie`함수의 인자값이어야 한다.
2. 코드를 한 줄로 간결하게 작성해 보자.

```solidity
pragma solidity ^0.4.19;

contract ZombieFactory {

    uint dnaDigits = 16;
    uint dnaModulus = 10 ** dnaDigits;

    struct Zombie {
        string name;
        uint dna;
    }

    Zombie[] public zombies;

    function createZombie(string _name, uint _dna) {
        zombies.push(Zombie(_name, _dna));
    }

}
```

</aside>

## **Private / Public 함수**

- 솔리디티에서 함수는 기본적으로 `public`으로 선언된다.
    - 누구나 (혹은 다른 어느 컨트랙트가) 내 컨트랙트의 함수를 호출하고 코드를 실행할 수 있으므로 공격에 취약해진다.
- 기본적으로 함수를 `private`으로 선언하고, 공개할 함수만 `public`으로 선언하는 것이 좋다.

### private 함수

컨트랙트 내의 다른 함수들만이 이 함수를 호출하여 `numbers` 배열로 무언가를 추가할 수 있다.

`private` 키워드는 함수명 다음에 적으며, 함수 인자명과 마찬가지로 private 함수명도 언더바(`_`)로 시작하는 것이 관례이다.

```solidity
uint[] numbers;

function _addToArray(uint _number) private {
  numbers.push(_number);
}
```

<aside>
⭐ 실습

우리 컨트랙트의 `createZombie` 함수는 현재 기본적으로 public으로 선언되어 있네. 즉, 누구나 이 함수를 호출해서 새로운 좀비를 컨트랙트에서 만들 수 있다는 뜻이지! 이 함수를 private로 선언해 보세.

1. `createZombie` 함수를 변경하여 private 함수로 만든다. 함수명에 대한 관례를 잊지 말 것!

```solidity
pragma solidity ^0.4.19;

contract ZombieFactory {

    uint dnaDigits = 16;
    uint dnaModulus = 10 ** dnaDigits;

    struct Zombie {
        string name;
        uint dna;
    }

    Zombie[] public zombies;

    function _createZombie(string _name, uint _dna) private {
        zombies.push(Zombie(_name, _dna));
    }

}
```

</aside>

## 함수 더 알아보기

### 함수의 **반환값**

솔리디티에서 함수 선언은 반환값 종류를 포함한다. (아래 경우에는 `string`)

```solidity
string greeting = "What's up dog";

function sayHello() public returns (string) {
  return greeting;
}
```

### 함수 제어자

1. view

위 함수 sayHello()는 솔리디티에서 상태를 변화시키지 않는다.(어떤 값을 변경하거나 무언가를 쓰지 않는다.)

이 경우에는 함수를 **view 함수**로 선언한다. 함수가 데이터를 보기만 하고 변경하지 않는다는 뜻이다.

```solidity
function sayHello() public view returns (string) {
		return greeting;
}
```

1. pure

함수가 앱에서 어떤 데이터도 접근하지 않는 것을 의미한다.

아래 함수는 앱에서 읽는 것도 하지 않고, 반환값이 함수에 전달된 인자값에 따라서 달라지므로 **pure 함수**로 선언한다.

```solidity
function _multiply(uint a, uint b) private pure returns (uint) {
	  return a * b;
}
```

<aside>
⭐ 실습

스트링으로부터 랜덤 DNA 숫자를 생성하는 도우미 함수가 필요할 걸세.

1. `_generateRandomDna`라는 `private` 함수를 만드시게. 이 함수는 `_str` (`string`형)을 인자로 전달받고, `uint`을 반환해야 하네.
2. 이 함수는 컨트랙트 변수를 보지만 변경하지는 않을 것이므로 `view`로 선언하게.
3. 이 함수의 내용은 현재로서는 비어 있는 상태로 냅두시게. 나중에 채울 것이네.

```solidity
pragma solidity ^0.4.19;

contract ZombieFactory {

    uint dnaDigits = 16;
    uint dnaModulus = 10 ** dnaDigits;

    struct Zombie {
        string name;
        uint dna;
    }

    Zombie[] public zombies;

    function _createZombie(string _name, uint _dna) private {
        zombies.push(Zombie(_name, _dna));
    }

    function _generateRandomDna(string _str) private view returns (uint) {
        
    }

}
```

</aside>

## **Keccak256과 형 변환**

### keccak256

이더리움은 SHA3의 한 버전인 `keccak256`를 내장 해시 함수로 가지고 있다.

해시 함수는 기본적으로 입력 스트링을 랜덤 256비트 16진수로 매핑한다.

의사 난수 발생기(pseudo-random number generator)로 이용할 수 있다.

이 방법은 별로 안전하지 않다.

```solidity
//6e91ec6b618bb462a4a6ee5aa2cb0e9cf30f7a052bb467b0ba58b8748c00d2e5
keccak256("aaaab");
//b1f078126895a1424524de5321b339ab00408010b7cf0e6ed451514981e58aa9
keccak256("aaaac");
```

### 형 변환

```solidity
uint8 a = 5;
uint b = 6;
// a * b가 uint8이 아닌 uint를 반환하기 때문에 에러 메시지가 난다
uint8 c = a * b; 
// b를 uint8으로 형 변환해서 코드가 제대로 작동하도록 해야 한다
uint8 c = a * uint8(b); 
```

<aside>
⭐ 실습

`_generateRandomDna` 함수의 내용을 채워 보세! 여기에 함수가 무엇을 해야 하는지 나와 있네:

1. 코드 첫 줄에서는 `_str`을 이용한 `keccak256` 해시값을 받아서 의사 난수 16진수를 생성하고 이를 `uint`로 형 변환한 다음, `rand`라는 `uint`에 결과값을 저장해야 한다.
2. 우리는 좀비의 DNA가 16자리 숫자이기만을 원하므로(`dnaModulus`를 기억하나?) 코드의 두번째 줄에서는 위의 결과 값을 모듈로(`%`) `dnaModulus`로 연산한 값을 반환해야 한다. (형식: `return` `위의 결과 값` `%` `dnaModulus`).

```solidity
pragma solidity ^0.4.19;

contract ZombieFactory {

    uint dnaDigits = 16;
    uint dnaModulus = 10 ** dnaDigits;

    struct Zombie {
        string name;
        uint dna;
    }

    Zombie[] public zombies;

    function _createZombie(string _name, uint _dna) private {
        zombies.push(Zombie(_name, _dna));
    } 

    function _generateRandomDna(string _str) private view returns (uint) {
        uint rand = uint(keccak256(_str));
        return rand % dnaModulus;
    }

}
```

</aside>

## 종합하기 실습

<aside>
⭐ 실습

랜덤 좀비 생성기를 거의 다 완성해 가는군! 이제 모든 내용을 종합하는 public 함수를 생성해 보세.

좀비의 이름을 입력값으로 받아 랜덤 DNA를 가진 좀비를 만드는 public 함수를 생성할 걸세.

1. `createRandomZombie`라는 `public`함수를 생성한다. 이 함수는 `_name`이라는 `string`형 인자를 하나 전달받는다. *(참고: 함수를 `private`로 선언한 것과 마찬가지로 함수를 `public`로 생성할 것)*
2. 이 함수의 첫 줄에서는 `_name`을 전달받은 `_generateRandomDna` 함수를 호출하고, 이 함수의 반환값을 `randDna`라는 `uint`형 변수에 저장해야 한다.
3. 두번째 줄에서는 `_createZombie` 함수를 호출하고 이 함수에 `_name`와 `randDna`를 전달해야 한다.
4. 함수의 내용을 닫는 `}`를 포함해서 코드가 4줄이어야 한다.

```solidity
pragma solidity ^0.4.19;

contract ZombieFactory {

    uint dnaDigits = 16;
    uint dnaModulus = 10 ** dnaDigits;

    struct Zombie {
        string name;
        uint dna;
    }

    Zombie[] public zombies;

    function _createZombie(string _name, uint _dna) private {
        zombies.push(Zombie(_name, _dna));
    } 

    function _generateRandomDna(string _str) private view returns (uint) {
        uint rand = uint(keccak256(_str));
        return rand % dnaModulus;
    }

    function createRandomZombie(string _name) public {
        uint randDna = _generateRandomDna(_name);
        _createZombie(_name, randDna);
    }

}
```

</aside>

## 이벤트

컨트랙트가 앱의 사용자 단에서 무언가 액션이 발생했을 때 의사소통하는 방법이다.
컨트랙트는 특정 이벤트가 일어나는지 "귀를 기울이고" 그 이벤트가 발생하면 행동을 취한다.

```solidity
// 이벤트를 선언한다
event IntegersAdded(uint x, uint y, uint result);

function add(uint _x, uint _y) public {
  uint result = _x + _y;
  // 이벤트를 실행하여 앱에게 add 함수가 실행되었음을 알린다:
  IntegersAdded(_x, _y, result);
  return result;
}
```

앱 기준 자바스크립트 코드

```jsx
YourContract.IntegersAdded(function(error, result) {
  // 결과와 관련된 행동을 취한다
})
```

<aside>
⭐ 실습

좀비가 생성될 때마다 우리 앱의 사용자 단에서 이에 대해 알고, 이를 표시하도록 하는 이벤트가 있으면 좋겠네.

1. `NewZombie`라는 `event`를 선언한다. `zombieId` (`uint`형), `name` (`string`형), `dna` (`uint`형)을 인자로 전달받아야 한다.
2. `_createZombie` 함수를 변경하여 새로운 좀비가 `zombies` 배열에 추가된 후에 `NewZombie` 이벤트를 실행하도록 한다.
3. 이벤트를 위해 좀비의 `id`가 필요할 것이다. `array.push()`는 배열의 새로운 길이를 `uint`형으로 반환한다. 배열의 첫 원소가 0이라는 인덱스를 갖기 때문에, `array.push() - 1`은 막 추가된 좀비의 인덱스가 될 것이다. `zombies.push() - 1`의 결과값을 `uint`형인 `id`로 저장하고 이를 다음 줄에서 `NewZombie` 이벤트를 위해 활용한다.

```solidity
pragma solidity ^0.4.19;

contract ZombieFactory {

    event NewZombie(uint zombieId, string name, uint dna);

    uint dnaDigits = 16;
    uint dnaModulus = 10 ** dnaDigits;

    struct Zombie {
        string name;
        uint dna;
    }

    Zombie[] public zombies;

    function _createZombie(string _name, uint _dna) private {
        uint id = zombies.push(Zombie(_name, _dna)) - 1;
        NewZombie(id, _name, _dna);
    }

    function _generateRandomDna(string _str) private view returns (uint) {
        uint rand = uint(keccak256(_str));
        return rand % dnaModulus;
    }

    function createRandomZombie(string _name) public {
        uint randDna = _generateRandomDna(_name);
        _createZombie(_name, randDna);
    }

}
```

</aside>

![image](https://github.com/naryeong-ko/TIL/assets/99406992/26f4ecb3-ed96-4c3a-bf30-7ea5fce86fe7)
