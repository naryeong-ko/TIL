귀여운 고양이나 무지개는 레슨 3에 없어!

라고 시작 전에 경고 받았다🙀

## 컨트랙트의 불변성

이더리움에 컨트랙트를 배포하고 나면, 컨트랙트는 **변하지 않는다(Immutable)**.(수정/업데이트 불가)

## 외부 의존성

버그로 인해 컨트렉트에 문제가 발생할 수 있기 때문에 DApp의 중요한 일부를 수정할 수 있도록 하는 함수를 만들어 놓는 것이 합리적이다.

ex. DApp에 크립토키티 컨트랙트 주소를 직접 써넣는 것 대신, 언젠가 크립토키티 컨트랙트에 문제가 생기면 해당 주소를 바꿀 수 있도록 해주는 `setKittyContractAddress` 함수 만들기

<aside>
⭐ 실습

레슨 2에서 우리가 만든 코드를 크립토키티 컨트랙트 주소의 업데이트가 가능하도록 바꿔보세.

1. 우리가 직접 주소를 써넣었던 `ckAddress`가 있는 줄을 지우게.
2. 우리가 `kittyContract`를 생성했던 줄을 변수 선언만 하도록 변경하게 - 어떤 것도 대입을 하지 않도록 하게.
3. `setKittyContractAddress`라는 이름의 함수를 생성하게. 이 함수는 `address` 타입의 변수 `_address`를 하나의 인자로 받고, `external` 함수여야 하네.
4. 함수 내에서, `kittyContract`에 `KittyInterface(_address)`를 대입하는 한 줄의 코드를 작성하게.

> 참고: 자네가 이 함수에서 보안 취약점을 발견했더라도, 걱정하지 말게 - 우린 다음 챕터에서 그걸 고칠 것이네 ;)
> 

```solidity
pragma solidity ^0.4.19;

import "./zombiefactory.sol";

contract KittyInterface {
  function getKitty(uint256 _id) external view returns (
    bool isGestating,
    bool isReady,
    uint256 cooldownIndex,
    uint256 nextActionAt,
    uint256 siringWithId,
    uint256 birthTime,
    uint256 matronId,
    uint256 sireId,
    uint256 generation,
    uint256 genes
  );
}

contract ZombieFeeding is ZombieFactory {

  // 1. 이 줄을 지우게:
  
  // 2. 여기서 대입을 빼고 그냥 선언으로 바꾸게:
  KittyInterface kittyContract;

  // 3. 여기 setKittyContractAddress 메소드를 추가하게
  function setKittyContractAddress(address _address) external {
    kittyContract = KittyInterface(_address);
  }

  function feedAndMultiply(uint _zombieId, uint _targetDna, string _species) public {
    require(msg.sender == zombieToOwner[_zombieId]);
    Zombie storage myZombie = zombies[_zombieId];
    _targetDna = _targetDna % dnaModulus;
    uint newDna = (myZombie.dna + _targetDna) / 2;
    if (keccak256(_species) == keccak256("kitty")) {
      newDna = newDna - newDna % 100 + 99;
    }
    _createZombie("NoName", newDna);
  }

  function feedOnKitty(uint _zombieId, uint _kittyId) public {
    uint kittyDna;
    (,,,,,,,,,kittyDna) = kittyContract.getKitty(_kittyId);
    feedAndMultiply(_zombieId, kittyDna, "kitty");
  }

}

```

### 보안 취약점

`setKittyContractAddress` 함수는 `external`이라, 누구든 이 함수를 호출할 수 있다.

따라서 아무나 이 함수를 호출해서 크립트키티 컨트랙트의 주소를 바꿀 수 있다.

</aside>

## 소유 가능한 컨트랙트

### `Ownable` 컨트랙트

OpenZeppelin 솔리디티 라이브러리의 `Ownable` 컨트랙트.

```solidity
/**
 * @title Ownable
 * @dev The Ownable contract has an owner address, and provides basic authorization control
 * functions, this simplifies the implementation of "user permissions".
 */
contract Ownable {
  address public owner;
  event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

/**
   * @dev The Ownable constructor sets the original `owner` of the contract to the sender
   * account.
   */
	function Ownable() public {
    owner = msg.sender;
	}

/**
   * @dev Throws if called by any account other than the owner.
   */
	modifier onlyOwner() {
    require(msg.sender == owner);
    _;
  }

/**
   * @dev Allows the current owner to transfer control of the contract to a newOwner.
   * @param newOwner The address to transfer ownership to.
   */
	function transferOwnership(address newOwner) public onlyOwner {
    require(newOwner != address(0));
    OwnershipTransferred(owner, newOwner);
    owner = newOwner;
  }
}

```

### 생성자(Constructor)

- 컨트랙트와 동일한 이름을 가진,생략할 수 있는 특별한 함수이다.
- 컨트랙트가 생성될 때 딱 한 번만 실행된다.
- 위 코드에서는  `function Ownable()`

### 함수 제어자(Function Modifier)

- 제어자는 다른 함수들에 대한 접근을 제어하기 위해 사용되는 일종의 유사 함수이다.
- 보통 함수 실행 전의 요구사항 충족 여부를 확인하는 데에 사용한다.
- `onlyOwner`의 경우에는 접근을 제한해서 **오직** 컨트랙트의 **소유자**만 해당 함수를 실행할 수 있도록 하기 위해 사용될 수 있다.
- 위 코드에서 함수 제어자는  `modifier onlyOwner()`

### `Ownable` 컨트랙트가 하는 행동

1. 컨트랙트가 생성되면 컨트랙트의 생성자가 `owner`에 `msg.sender`(컨트랙트를 배포한 사람)를 대입한다.
2. 특정한 함수들에 대해서 오직 `소유자`만 접근할 수 있도록 제한 가능한 `onlyOwner` 제어자를 추가한다.
3. 새로운 `소유자`에게 해당 컨트랙트의 소유권을 옮길 수 있도록 한다.

`onlyOwner`는 컨트랙트에서 흔히 쓰는 것 중 하나라, 대부분의 솔리디티 DApp들은 `Ownable` 컨트랙트를 복사/붙여넣기 하면서 시작한다. 그리고 첫 컨트랙트는 이 컨트랙트를 상속해서 만든다.

<aside>
⭐ 실습

우리가 먼저 `Ownable` 컨트랙트의 코드를 `ownable.sol`이라는 새로운 파일로 복사해놨다네. 어서 `ZombieFactory`가 이걸 상속받도록 만들어보게.

1. 우리 코드가 `ownable.sol`의 내용을 `import`하도록 수정하게. 어떻게 하는지 기억이 나지 않는다면 `zombiefeeding.sol`을 살펴보게.
2. `ZombieFactory` 컨트랙트가 `Ownable`을 상속하도록 수정하게. 다시 말하지만, 이걸 어떻게 하는지 잘 기억나지 않는다면 `zombiefeeding.sol`을 살펴보게.

```solidity
pragma solidity ^0.4.19;

// 1. 여기서 import하게
import "./ownable.sol";

// 2. 상속을 추가하게:
contract ZombieFactory is Ownable {

    event NewZombie(uint zombieId, string name, uint dna);

    uint dnaDigits = 16;
    uint dnaModulus = 10 ** dnaDigits;

    struct Zombie {
        string name;
        uint dna;
    }

    Zombie[] public zombies;

    mapping (uint => address) public zombieToOwner;
    mapping (address => uint) ownerZombieCount;

    function _createZombie(string _name, uint _dna) internal {
        uint id = zombies.push(Zombie(_name, _dna)) - 1;
        zombieToOwner[id] = msg.sender;
        ownerZombieCount[msg.sender]++;
        NewZombie(id, _name, _dna);
    }

    function _generateRandomDna(string _str) private view returns (uint) {
        uint rand = uint(keccak256(_str));
        return rand % dnaModulus;
    }

    function createRandomZombie(string _name) public {
        require(ownerZombieCount[msg.sender] == 0);
        uint randDna = _generateRandomDna(_name);
        randDna = randDna - randDna % 100;
        _createZombie(_name, randDna);
    }

}

```

</aside>

## **함수 제어자**

- `function` 키워드 대신 `modifier` 키워드를 사용한다.
- 함수를 호출하듯이 직접 호출할 수 없다.
- 함수 정의부 끝에 해당 함수의 작동 방식을 바꾸도록 제어자의 이름을 붙일 수 있다.

아래는 `onlyOwner` 의 함수 제어자이다.

```solidity
/**
 * @dev Throws if called by any account other than the owner.
 */
modifier onlyOwner() {
  require(msg.sender == owner);
  _;
}

```

이 제어자를 다음과 같이 사용할 수 있다.

```solidity
contract MyContract is Ownable {
  event LaughManiacally(string laughter);

// 아래 `onlyOwner`의 사용 방법을 잘 보게
	function likeABoss() external onlyOwner {
    LaughManiacally("Muahahahaha");
  }
}

```

- `likeABoss` 함수를 호출하면, `onlyOwner`의 코드가 **먼저** 실행된다. 그리고 `onlyOwner`의 `_;` 부분을 `likeABoss` 함수로 되돌아가 해당 코드를 실행하게 된다.
- 제어자를 사용할 수 있는 다양한 방법이 있지만, 가장 일반적으로 쓰는 예시 중 하나는 함수 실행 전에 `require` 체크를 넣는 것이다.
- `onlyOwner`의 경우에는, 함수에 이 제어자를 추가하면 **오직** 컨트랙트의 **소유자**(컨트랙트를 배포한 사람)만이 해당 함수를 호출할 수 있다.

<aside>
⭐ 실습

이제 우리는 미래에 `setKittyContractAddress`를 우리만 수정할 수 있도록 제한할 수 있네.

1. `onlyOwner` 제어자를 `setKittyContractAddress`에 추가하게.

```solidity
pragma solidity ^0.4.19;

import "./zombiefactory.sol";

contract KittyInterface {
  function getKitty(uint256 _id) external view returns (
    bool isGestating,
    bool isReady,
    uint256 cooldownIndex,
    uint256 nextActionAt,
    uint256 siringWithId,
    uint256 birthTime,
    uint256 matronId,
    uint256 sireId,
    uint256 generation,
    uint256 genes
  );
}

contract ZombieFeeding is ZombieFactory {

  KittyInterface kittyContract;

  // 이 함수를 수정하게:
  function setKittyContractAddress(address _address) external onlyOwner {
    kittyContract = KittyInterface(_address);
  }

  function feedAndMultiply(uint _zombieId, uint _targetDna, string _species) public {
    require(msg.sender == zombieToOwner[_zombieId]);
    Zombie storage myZombie = zombies[_zombieId];
    _targetDna = _targetDna % dnaModulus;
    uint newDna = (myZombie.dna + _targetDna) / 2;
    if (keccak256(_species) == keccak256("kitty")) {
      newDna = newDna - newDna % 100 + 99;
    }
    _createZombie("NoName", newDna);
  }

  function feedOnKitty(uint _zombieId, uint _kittyId) public {
    uint kittyDna;
    (,,,,,,,,,kittyDna) = kittyContract.getKitty(_kittyId);
    feedAndMultiply(_zombieId, kittyDna, "kitty");
  }

}

```

</aside>

## 가스

이더리움 DApp이 사용하는 연료

- 솔리디티에서는 사용자들이 DApp의 함수를 실행할 때마다 **가스**라고 불리는 화폐를 지불해야 한다.
- 사용자는 이더(ETH, 이더리움의 화폐)를 이용해서 가스를 사기 때문에, DApp 함수를 실행하려면 사용자들은 ETH를 소모해야 한다.
- 각각의 연산은 소모되는 **가스 비용(gas cost)**이 있고, 그 연산을 수행하는 데에 소모되는 컴퓨팅 자원의 양이 이 비용을 결정한다.
    - storage에 값을 쓰는 것은 두 개의 정수를 더하는 것보다 훨씬 비용이 높다.
- 함수의 전체 **가스 비용**은 그 함수를 구성하는 개별 연산들의 가스 비용을 모두 합친 것과 같다.

### 가스는 왜 필요한가?

이더리움에서 어떤 함수를 실행할 때, 네트워크상의 모든 개별 노드가 함수의 출력값을 검증하기 위해 그 함수를 실행해야 한다. 모든 함수의 실행을 검증하는 수천 개의 노드가 바로 이더리움을 분산화하고, 데이터를 보존하며 누군가 검열할 수 없도록 하는 요소이다.

누군가가 무한 반복문을 써서 네트워크를 방해하거나, 자원 소모가 큰 연산을 써서 네트워크 자원을 모두 사용하지 못하도록 만들길 원했기 때문에 연산 처리에 비용이 들도록 만들었고, 사용자들은 저장 공간 뿐만 아니라 연산 사용 시간에 따라서도 비용을 지불해야 한다.

> 참고: 사이드체인에서는 반드시 이렇지는 않다.
> 

### 가스를 아끼기 위한 구조체 압축

- **`struct` 가 아닐 때**
    - 솔리디티에서는 `uint`의 크기에 상관없이 256비트의 저장 공간을 미리 잡아놓기 때문에  `uint8`, `uint16`, `uint32`같은 하위 타입들을 쓰는 것은 기본적으로는 가스 소모를 줄이는 데에 아무런 이득이 없다.
- **`struct` 안일 때**
    - **가능한 더 작은 크기의 변수를 사용**하는 것이 좋다. 솔리디티에서 그 변수들을 더 적은 공간을 차지하도록 압축할 것이기 때문이다.
    - **동일한 데이터 타입은 하나로 묶어**놓는 것이 좋다. 구조체에서 서로 옆에 있도록 선언하면 솔리디티에서 사용하는 저장 공간을 최소화하기 때문이다.
        
        ex. `uint c; uint32 a; uint32 b;`라는 필드로 구성된 구조체는 `uint32` 필드가 묶여있기 때문에 `uint32 a; uint c; uint32 b;` 필드로 구성된 구조체보다 가스를 덜 소모한다.
        

```solidity
struct NormalStruct {
  uint a;
  uint b;
  uint c;
}

struct MiniMe {
  uint32 a;
  uint32 b;
  uint c;
}

// `mini`는 구조체 압축을 했기 때문에 `normal`보다 가스를 조금 사용할 것
NormalStruct normal = NormalStruct(10, 20, 30);
MiniMe mini = MiniMe(10, 20, 30);

```

<aside>
⭐ 실습

이번 레슨에서는, 우리는 우리의 좀비에게 2개의 새로운 특징을 추가할 것이네. `level`과 `readyTime`이지. `readyTime`은 좀비가 먹이를 먹는 빈도를 제한할 재사용 대기 시간을 구현하기 위해 사용하네.

자, 그럼 다시 `zombiefactory.sol`으로 돌아가지.

1. 우리의 `Zombie` 구조체에 2개의 속성을 더 추가하게: `level`(`uint32`)과 `readyTime`(마찬가지로 `uint32`)를 말이지. 우리는 이 데이터 타입들을 압축하길 원하니, 이 둘을 구조체의 마지막 부분에 쓰게.

좀비의 레벨과 시간 데이터(Timestamp)를 저장하는 데에는 충분하고도 남는 크기이니, 이렇게 하면 보통의 `uint`(256비트)를 쓰는 것보다 데이터를 더 압축해서 가스 비용을 줄이도록 해줄 것이네.

```solidity
pragma solidity ^0.4.19;

import "./ownable.sol";

contract ZombieFactory is Ownable {

    event NewZombie(uint zombieId, string name, uint dna);

    uint dnaDigits = 16;
    uint dnaModulus = 10 ** dnaDigits;

    struct Zombie {
        string name;
        uint dna;
        uint32 level;
        uint32 readyTime;
    }

    Zombie[] public zombies;

    mapping (uint => address) public zombieToOwner;
    mapping (address => uint) ownerZombieCount;

    function _createZombie(string _name, uint _dna) internal {
        uint id = zombies.push(Zombie(_name, _dna)) - 1;
        zombieToOwner[id] = msg.sender;
        ownerZombieCount[msg.sender]++;
        NewZombie(id, _name, _dna);
    }

    function _generateRandomDna(string _str) private view returns (uint) {
        uint rand = uint(keccak256(_str));
        return rand % dnaModulus;
    }

    function createRandomZombie(string _name) public {
        require(ownerZombieCount[msg.sender] == 0);
        uint randDna = _generateRandomDna(_name);
        randDna = randDna - randDna % 100;
        _createZombie(_name, randDna);
    }

}

```

</aside>

## 시간 단위(Time units)

- 솔리디티는 시간을 다룰 수 있는 단위계를 기본적으로 제공한다.
- `now` 변수를 쓰면 현재의 유닉스 타임스탬프(1970년 1월 1일부터 지금까지의 초 단위 합) 값을 얻을 수 있다.

> 참고: 유닉스 타임은 전통적으로 32비트 숫자로 저장된다. 이는 유닉스 타임스탬프 값이 32비트로 표시가 되지 않을 만큼 커졌을 때 많은 구형 시스템에 문제가 발생할 "Year 2038" 문제를 일으킬 것이다. 그러니 만약 DApp이 그 이후까지 운영되길 원한다면, 64비트 숫자를 써야 할 것이다. 하지만 유저들은 그동안 더 많은 가스를 소모해야 할 것이다. 설계를 보고 결정을 해야 한다!
> 
- `seconds`, `minutes`, `hours`, `days`, `weeks`, `years` 같은 시간 단위도 있다.
    - 이 단위들은 그에 해당하는 길이 만큼의 초 단위 `uint` 숫자로 변환된다.
        - `1 minutes`는 `60`, `1 hours`는 `3600`(60초 x 60 분), `1 days`는 `86400`(24시간 x 60분 x 60초)

```solidity
uint lastUpdated;

// `lastUpdated`를 `now`로 설정
function updateTimestamp() public {
  lastUpdated = now;
}

// 마지막으로 `updateTimestamp`가 호출된 뒤 5분이 지났으면 `true`를, 5분이 아직 지나지 않았으면 `false`를 반환
function fiveMinutesHavePassed() public view returns (bool) {
  return (now >= (lastUpdated + 5 minutes));
}

```

<aside>
⭐ 실습

우리 DApp에 재사용 대기 시간을 추가하고, 좀비들이 공격하거나 먹이를 먹은 후 **1일**이 지나야만 다시 공격할 수 있도록 할 것이네.

1. `cooldownTime`이라는 `uint` 변수를 선언하고, 여기에 `1 days`를 대입하게.(문법적으로 이상하게 보여도 넘어가게. 자네가 "1 day"를 대입한다면, 컴파일이 되지 않을 것일세!)
2. 우리가 이전 챕터에서 우리의 `Zombie` 구조체에 `level`과 `readyTime`을 추가했으니, 우린 `Zombie` 구조체를 생성할 때 함수의 인수 개수가 정확히 맞도록 `_createZombie()` 함수를 업데이트해야 하네.
    
    코드의 `zombies.push` 줄에 2개의 인수를 더 사용하도록 업데이트하게: `1`(`level`에 사용), `uint32(now + cooldownTime)`(`readyTime`에 사용).
    

> 참고: now가 기본적으로 uint256을 반환하기 때문에, uint32(...) 부분이 필수적이네. 이렇게 함으로써 해당 데이터를 uint32로 명시적으로 변환하는 것이지.
> 

`now + cooldownTime`은 현재 유닉스 타임스탬프(초 단위)에 1일을 초 단위로 바꾼 것의 합과 같을 것이네. 바꿔 말해 지금부터 하루 뒤의 유닉스 타임스탬프 값과 같은 것이지. 이후에 우리는 좀비를 다시 사용하기 위해 충분한 시간이 지났는지 확인할 수 있도록 좀비의 `readyTime`이 `now`보다 큰지 비교할 것이네.

다음 챕터에서는 `readyTime`에 기반하여 행동을 제한하도록 하는 기능들을 구현할 것이네.

```solidity
pragma solidity ^0.4.19;

import "./ownable.sol";

contract ZombieFactory is Ownable {

    event NewZombie(uint zombieId, string name, uint dna);

    uint dnaDigits = 16;
    uint dnaModulus = 10 ** dnaDigits;
    // 1. `cooldownTime`을 여기에 정의하게
    uint cooldownTime = 1 days;

    struct Zombie {
        string name;
        uint dna;
        uint32 level;
        uint32 readyTime;
    }

    Zombie[] public zombies;

    mapping (uint => address) public zombieToOwner;
    mapping (address => uint) ownerZombieCount;

    function _createZombie(string _name, uint _dna) internal {
        // 2. 아래 줄을 업데이트하게:
        uint id = zombies.push(Zombie(_name, _dna, 1, uint32(now + cooldownTime))) - 1;
        zombieToOwner[id] = msg.sender;
        ownerZombieCount[msg.sender]++;
        NewZombie(id, _name, _dna);
    }

    function _generateRandomDna(string _str) private view returns (uint) {
        uint rand = uint(keccak256(_str));
        return rand % dnaModulus;
    }

    function createRandomZombie(string _name) public {
        require(ownerZombieCount[msg.sender] == 0);
        uint randDna = _generateRandomDna(_name);
        randDna = randDna - randDna % 100;
        _createZombie(_name, randDna);
    }

}

```

</aside>

## 구조체를 인수로 전달하기

`private` 또는 `internal` 함수에 인수로서 구조체의 storage 포인터를 전달할 수 있다.

```solidity
function _doStuff(Zombie storage _zombie) internal {
// _zombie로 할 수 있는 것들을 처리
}

```

이런 방식으로 함수에 좀비ID를 전달하고 좀비를 찾는 대신, 좀비에 대한 참조를 전달할 수 있다.

<aside>
⭐ 실습

1. `_triggerCooldown`을 정의하면서 시작하지. 이 함수는 1개의 인수로 `Zombie storage` 포인터 타입인 `_zombie`를 받네. 이 함수는 `internal`이어야 하네.
2. 함수의 내용에서는 `_zombie.readyTime`을 `uint32(now + cooldownTime)`으로 설정해야 하네.
3. 다음으로, `_isReady`라고 불리는 함수를 만들게. 이 함수 역시 `_zombie`라는 이름의 `Zombie storage` 타입 인수를 받네. `internal view`여야 하고, `bool`을 리턴해야 하네.
4. 함수의 내용에서는 `(_zombie.readyTime <= now)`를 리턴해야 하고, 이는 `true` 아니면 `false`로 계산될 것이네. 이 함수는 우리에게 좀비가 먹이를 먹은 후 충분한 시간이 지났는지 알려줄 것이네.

```solidity
pragma solidity ^0.4.19;

import "./zombiefactory.sol";

contract KittyInterface {
  function getKitty(uint256 _id) external view returns (
    bool isGestating,
    bool isReady,
    uint256 cooldownIndex,
    uint256 nextActionAt,
    uint256 siringWithId,
    uint256 birthTime,
    uint256 matronId,
    uint256 sireId,
    uint256 generation,
    uint256 genes
  );
}

contract ZombieFeeding is ZombieFactory {

  KittyInterface kittyContract;

  function setKittyContractAddress(address _address) external onlyOwner {
    kittyContract = KittyInterface(_address);
  }

  // 1. `_triggerCooldown` 함수를 여기에 정의하게
  function _triggerCooldown(Zombie storage _zombie) internal {
    _zombie.readyTime = uint32(now + cooldownTime);
  }
  // 2. `_isReady` 함수를 여기에 정의하게
  function _isReady(Zombie storage _zombie) internal view returns (bool) {
    return (_zombie.readyTime <= now);
  }

  function feedAndMultiply(uint _zombieId, uint _targetDna, string _species) public {
    require(msg.sender == zombieToOwner[_zombieId]);
    Zombie storage myZombie = zombies[_zombieId];
    _targetDna = _targetDna % dnaModulus;
    uint newDna = (myZombie.dna + _targetDna) / 2;
    if (keccak256(_species) == keccak256("kitty")) {
      newDna = newDna - newDna % 100 + 99;
    }
    _createZombie("NoName", newDna);
  }

  function feedOnKitty(uint _zombieId, uint _kittyId) public {
    uint kittyDna;
    (,,,,,,,,,kittyDna) = kittyContract.getKitty(_kittyId);
    feedAndMultiply(_zombieId, kittyDna, "kitty");
  }

}

```

</aside>

## Public 함수 & 보안

- 모든 `public`과 `external` 함수를 검사하고, 사용자들이 그 함수들을 남용할 수 있는 방법을 생각해보면서 보안을 점검하면 좋다.
- 이 함수들이 `onlyOwner` 같은 제어자를 갖지 않는 이상, 어떤 사용자든 이 함수들을 호출하고 자신들이 원하는 모든 데이터를 함수에 전달할 수 있다는 것을 기억해야 한다.
- 남용을 막을 쉬운 방법 중 하나는 함수를 `internal`로 만드는 것이다.

<aside>
⭐ 실습

1. 현재 `feedAndMultiply`는 `public` 함수이네. 이걸 `internal`로 만들어서 컨트랙트가 더 안전해지도록 하세. 우리는 사용자들이 그들이 원하는 아무 DNA나 넣어서 이 함수를 실행하는 것을 원하지 않네.
2. `feedAndMultiply` 함수가 `cooldownTime`을 고려하도록 만들어보세. 먼저, `myZombie`를 찾은 후에, `_isReady()`를 확인하는 `require` 문장을 추가하고 거기에 `myZombie`를 전달하게. 이렇게 하면 사용자들은 좀비의 재사용 대기 시간이 끝난 다음에만 이 함수를 실행할 수 있네.
3. 함수의 끝에서 `_triggerCooldown(myZombie)` 함수를 호출하여 먹이를 먹는 것이 좀비의 재사용 대기 시간을 만들도록 하게.

```solidity
pragma solidity ^0.4.19;

import "./zombiefactory.sol";

contract KittyInterface {
  function getKitty(uint256 _id) external view returns (
    bool isGestating,
    bool isReady,
    uint256 cooldownIndex,
    uint256 nextActionAt,
    uint256 siringWithId,
    uint256 birthTime,
    uint256 matronId,
    uint256 sireId,
    uint256 generation,
    uint256 genes
  );
}

contract ZombieFeeding is ZombieFactory {

  KittyInterface kittyContract;

  function setKittyContractAddress(address _address) external onlyOwner {
    kittyContract = KittyInterface(_address);
  }

  function _triggerCooldown(Zombie storage _zombie) internal {
    _zombie.readyTime = uint32(now + cooldownTime);
  }

  function _isReady(Zombie storage _zombie) internal view returns (bool) {
      return (_zombie.readyTime <= now);
  }

  // 1. 이 함수를 internal로 만들게
  function feedAndMultiply(uint _zombieId, uint _targetDna, string _species) internal {
    require(msg.sender == zombieToOwner[_zombieId]);
    Zombie storage myZombie = zombies[_zombieId];
    // 2. 여기에 `_isReady`를 확인하는 부분을 추가하게
    require(_isReady(myZombie));
    _targetDna = _targetDna % dnaModulus;
    uint newDna = (myZombie.dna + _targetDna) / 2;
    if (keccak256(_species) == keccak256("kitty")) {
      newDna = newDna - newDna % 100 + 99;
    }
    _createZombie("NoName", newDna);
    // 3. `_triggerCooldown`을 호출하게
    _triggerCooldown(myZombie);
  }

  function feedOnKitty(uint _zombieId, uint _kittyId) public {
    uint kittyDna;
    (,,,,,,,,,kittyDna) = kittyContract.getKitty(_kittyId);
    feedAndMultiply(_zombieId, kittyDna, "kitty");
  }

}

```

</aside>

## 인수를 가지는 함수 제어자

아래 코드에서 `olderthan` 제어자가 함수와 비슷하게 인수를 받는 것을 볼 수 있다. 그리고 `driveCar` 함수는 받은 인수를 제어자로 전달하고 있다.

```solidity
// 사용자의 나이를 저장하기 위한 매핑
mapping (uint => uint) public age;

// 사용자가 특정 나이 이상인지 확인하는 제어자
modifier olderThan(uint _age, uint _userId) {
  require (age[_userId] >= _age);
  _;
}

// 차를 운전하기 위햐서는 16살 이상이어야 하네(적어도 미국에서는).
// `olderThan` 제어자를 인수와 함께 호출하려면 이렇게 하면 되네
function driveCar(uint _userId) public olderThan(16, _userId) {
// 필요한 함수 내용들
}
```

<aside>
⭐ 실습

1. `ZombieHelper`에서, `aboveLevel`이라는 이름의 `modifier`를 만들게. 이 제어자는 `_level`(`uint`), `_zombieId`(`uint`) 두 개의 인수를 받을 것이네.
2. 함수 내용에서는 `zombies[_zombieId].level`이 `_level` 이상인지 확실하게 확인해야 하네.
3. 함수의 나머지 내용을 실행할 수 있도록 제어자의 마지막 줄에 `_;`를 넣는 것을 잊지 말게.

```solidity
pragma solidity ^0.4.19;

import "./zombiefeeding.sol";

contract ZombieHelper is ZombieFeeding {

  // 여기서 시작하게
  modifier aboveLevel(uint _level, uint _zombieId) {
    require (zombies[_zombieId].level >= _level);
    _;
  }
}

```

</aside>

<aside>
⭐ 실습

이제 몇몇 함수를 만들 때 우리의 `aboveLevel` 제어자를 사용해보세.

우리 게임에서는 사용자들이 그들의 좀비를 레벨업할 때 인센티브를 줄 것이네.

- 레벨 2 이상인 좀비인 경우, 사용자들은 그 좀비의 이름을 바꿀 수 있네.
- 레벨 20 이상인 좀비인 경우, 사용자들은 그 좀비에게 임의의 DNA를 줄 수 있네.
1. `changeName`이라는 함수를 만들게. 이 함수는 2개의 인수를 받을 것이네: `_zombieId`(`uint`), `_newName`(`string`). 그리고 함수를 `external`로 만들게. 이 함수는 `aboveLevel` 제어자를 가져야 하고, `_level`에 `2`라는 값을 전달해야 하네. `_zombieId` 또한 전달하는 것을 잊지 말게나.
2. 함수의 내용에서는, 먼저 우리는 `msg.sender`가 `zombieToOwner[_zombieId]`와 같은지 검증해야 하네. `require` 문장을 사용하게.
3. 그리고 나서 이 함수에서는 `zombies[_zombieId].name`에 `_newName`을 대입해야 하네.
4. `changeName` 아래에 `changeDna`라는 또다른 함수를 만들게. 그리고 함수를 `external`로 만들게. 이 함수의 정의와 내용은 `changeName`과 거의 똑같지만, 두 번째 인수가 `_newDna`(`uint`)이고, `aboveLevel`의 `_level` 매개 변수에 `20`을 전달해야 할 것이네. 물론, 이 함수는 좀비의 이름을 설정하는 것 대신에 좀비의 `dna`를 `_newDna`로 설정해야 하겠지.

```solidity
pragma solidity ^0.4.19;

import "./zombiefeeding.sol";

contract ZombieHelper is ZombieFeeding {

  modifier aboveLevel(uint _level, uint _zombieId) {
    require(zombies[_zombieId].level >= _level);
    _;
  }

  // 여기서 시작하게
  function changeName(uint _zombieId, string _newName) external aboveLevel(2, _zombieId) {
    require(msg.sender == zombieToOwner[_zombieId]);
    zombies[_zombieId].name = _newName;
  }

  function changeDna(uint _zombieId, uint _newDna) external aboveLevel(20, _zombieId) {
    require(msg.sender == zombieToOwner[_zombieId]);
    zombies[_zombieId].dna = _newDna;
  }
}

```

</aside>

## 'View' 함수를 사용해 가스 절약하기

`view` 함수는 사용자에 의해 외부에서 호출되었을 때 가스를 전혀 소모하지 않는다.

- `view` 함수가 데이터를 읽기만 하고 블록체인 상에서 실제로 어떤 것도 수정하지 않기 떄문
- 함수에 `view` 표시를 하는 것은 이 함수는 실행할 때 로컬 이더리움 노드에 질의만 날리면 되고, 블록체인에 어떤 트랜잭션도 만들지 않는다는 것을 뜻한다.
    - 트랜잭션은 모든 개별 노드에서 실행되어야 하고, 가스를 소모한다.
- `view` 함수는 외부에서 호출됐을 때에만 무료이다.
- 가능한 모든 곳에 읽기 전용의 `external view` 함수를 쓰면 가스를 절약할 수 있다.

> 참고: view 함수가 동일 컨트랙트 내에 있는, view 함수가 아닌 다른 함수에서 내부적으로 호출될 경우, 가스를 소모할 것이다. 이것은 다른 함수가 이더리움에 트랜잭션을 생성하고, 이는 모든 개별 노드에서 검증되어야 하기 때문이다.
> 

<aside>
⭐ 실습

우리는 사용자의 전체 좀비 군대를 반환하는 함수를 구현할 것이네. 우리가 만약 사용자들의 프로필 페이지에 그들의 전체 군대를 표시하고 싶다면, 나중에 이 함수를 `web3.js`에서 호출하면 된다네.

이 함수의 내용은 조금 복잡해서, 구현하는 데에 챕터 몇 개를 써야 할 것이네.

1. `getZombiesByOwner`라는 이름의 함수를 만들게. 이 함수는 `_owner`라는 이름의 `address`를 하나의 인수로 받을 것이네.
2. 이걸 `external view` 함수로 만들게. 우리는 이 함수를 `web3.js`에서 가스를 쓸 필요 없이 호출할 수 있을 것이네.
3. 이 함수는 `uint[]`를 반환해야 하네(`uint`의 배열).

지금은 함수의 내용을 비워두게. 다음 챕터에서 채워나갈 것이네.

```solidity
pragma solidity ^0.4.19;

import "./zombiefeeding.sol";

contract ZombieHelper is ZombieFeeding {

  modifier aboveLevel(uint _level, uint _zombieId) {
    require(zombies[_zombieId].level >= _level);
    _;
  }

  function changeName(uint _zombieId, string _newName) external aboveLevel(2, _zombieId) {
    require(msg.sender == zombieToOwner[_zombieId]);
    zombies[_zombieId].name = _newName;
  }

  function changeDna(uint _zombieId, uint _newDna) external aboveLevel(20, _zombieId) {
    require(msg.sender == zombieToOwner[_zombieId]);
    zombies[_zombieId].dna = _newDna;
  }

  // 자네의 함수를 여기에 만들게
  function getZombiesByOwner(address _owner) external view returns (uint[]){

  }
}

```

</aside>

## Storage는 비싸다

솔리디티에서 비싼 연산 중 하나는 바로 `storage`를 쓰는 것이다.(특히 쓰기 연산)

- 데이터의 일부를 쓰거나 바꿀 때마다, 블록체인에 영구적으로 기록되기 때문이다.
    - 지구상의 수천 개의 노드들이 그들의 하드 드라이브에 그 데이터를 저장해야 하고, 블록체인이 커져가면서 이 데이터의 양 또한 같이 커져가기 때문에 이 연산에는 비용이 든다.
- 비용을 최소화하기 위해서, 진짜 필요한 경우가 아니면 storage에 데이터를 쓰지 않는 것이 좋다.
    - 이를 위해 때때로는 겉보기에 비효율적으로 보이는 프로그래밍 구성을 할 필요가 있다.
        
        ex. 어떤 배열에서 내용을 빠르게 찾기 위해, 단순히 변수에 저장하는 것 대신 함수가 호출될 때마다 배열을 `memory`에 다시 만드는 것
        
- 솔리디티에서는 큰 데이터 집합의 개별 데이터에 모두 접근하는 것이 `external view` 함수라면 `storage`를 사용하는 것보다 더 저렴한 방법이다.
    - `view` 함수는 사용자들의 가스를 소모하지 않기 때문

## 메모리에 배열 선언하기

Storage에 아무것도 쓰지 않고도 함수 안에 새로운 배열을 만들기 위해 배열에 `memory` 키워드를 쓸 수 있다.

- 이 배열은 함수가 끝날 때까지만 존재할 것이고, 이는 `storage`의 배열을 직접 업데이트하는 것보다 가스 소모 측면에서 훨씬 저렴하다.
    - 외부에서 호출되는 `view` 함수라면 무료이다.

```solidity
function getArray() external pure returns(uint[]) {
// 메모리에 길이 3의 새로운 배열을 생성한다.
	uint[] memory values = new uint[](3);
// 여기에 특정한 값들을 넣는다.
  values.push(1);
  values.push(2);
  values.push(3);
// 해당 배열을 반환한다.
	return values;
}

```

> 참고: 메모리 배열은 반드시 길이 인수와 함께 생성되어야 한다.(위 예시에서는, 3). 메모리 배열은 현재로서는 storage 배열처럼 array.push()로 크기가 조절되지 않는다.
> 

<aside>
⭐ 실습

`getZombiesByOwner` 함수에서, 우리는 특정한 사용자가 소유한 모든 좀비를 `uint[]` 배열로 반환하기를 원하네.

1. `result`라는 이름의 `uint[] memory` 변수를 선언하게.
2. 해당 변수에 `uint` 배열을 대입하게. 배열의 길이는 이 `_owner`가 소유한 좀비의 개수여야 하고, 이는 우리의 `mapping`인 `ownerZombieCount[_owner]`를 통해서 찾을 수 있네.
3. 함수의 끝에서 `result`를 반환하게. 지금 당장은 빈 배열이지만, 다음 챕터에서 이를 채울 것이네.

```solidity
pragma solidity ^0.4.19;

import "./zombiefeeding.sol";

contract ZombieHelper is ZombieFeeding {

  modifier aboveLevel(uint _level, uint _zombieId) {
    require(zombies[_zombieId].level >= _level);
    _;
  }

  function changeName(uint _zombieId, string _newName) external aboveLevel(2, _zombieId) {
    require(msg.sender == zombieToOwner[_zombieId]);
    zombies[_zombieId].name = _newName;
  }

  function changeDna(uint _zombieId, uint _newDna) external aboveLevel(20, _zombieId) {
    require(msg.sender == zombieToOwner[_zombieId]);
    zombies[_zombieId].dna = _newDna;
  }

  function getZombiesByOwner(address _owner) external view returns(uint[]) {
    uint[] memory result = new uint[](ownerZombieCount[_owner]);
    return result;
  }

}

```

</aside>

## `for` 반복문 사용하기

솔리디티에서 `for` 반복문의 문법은 자바스크립트의 문법과 비슷하다.

```solidity
// 짝수로 구성된 배열 만들기
function getEvens() pure external returns(uint[]) {
  uint[] memory evens = new uint[](5);
// 새로운 배열의 인덱스를 추적하는 변수
	uint counter = 0;
// for 반복문에서 1부터 10까지 반복함
	for (uint i = 1; i <= 10; i++) {
// `i`가 짝수라면...
		if (i % 2 == 0) {
// 배열에 i를 추가함
      evens[counter] = i;
// `evens`의 다음 빈 인덱스 값으로 counter를 증가시킴
      counter++;
    }
  }
  return evens;
}

// 이 함수는 [2, 4, 6, 8, 10]를 가지는 배열을 반환
```

<aside>
⭐ 실습

`for` 반복문을 써서 `getZombiesByOwner` 함수를 끝내보도록 하지. 반복문 안에서는 우리 DApp 안에 있는 모든 좀비들에 접근하고, 그들의 소유자가 우리가 찾는 자인지 비교하여 확인한 후, 조건에 맞는 좀비들을 `result` 배열에 추가한 후 반환할 것이네.

1. `counter`라는 이름의 `uint`를 하나 선언하고 `0`을 대입하게. 우린 `result` 배열에서 인덱스를 추적하기 위해 이 변수를 사용할 것이네.
2. `uint i = 0`에서 시작해서 `i < zombies.length`까지 증가하는 `for` 반복문을 선언하게. 이 반복문에서 우리 배열의 모든 좀비에 접근할 것이네.
3. `for` 반복문 안에서, `zombieToOwner[i]`가 `_owner`와 같은지 확인하는 `if` 문장을 만들게. 이 문장은 두 개의 주소값이 같은지 비교하는 것이네.
4. `if` 문장 안에서:
    1. `result[counter]`에 `i`를 대입해서 `result` 배열에 좀비의 ID를 추가하게.
    2. `counter`를 1 증가시키게(위의 `for` 반복문 예시를 참고하게).

이게 끝이라네 - 이 함수는 이제 `_owner`가 소유한 모든 좀비를 가스를 소모하지 않고 반환하게 될 것이네.

</aside>

![image](https://github.com/naryeong-ko/TIL/assets/99406992/4c79bbf8-a3cb-4a88-b0c9-2423247ca945)

![image](https://github.com/naryeong-ko/TIL/assets/99406992/e94fc108-29eb-48a7-b630-150d07983610)


